options {
  LOOKAHEAD = 1;
  CHOICE_AMBIGUITY_CHECK = 2;
  OTHER_AMBIGUITY_CHECK = 1;
  STATIC = true;
  DEBUG_PARSER = false;
  DEBUG_LOOKAHEAD = false;
  DEBUG_TOKEN_MANAGER = false;
  ERROR_REPORTING = true;
  JAVA_UNICODE_ESCAPE = false;
  UNICODE_INPUT = false;
  IGNORE_CASE = false;
  USER_TOKEN_MANAGER = false;
  USER_CHAR_STREAM = false;
  BUILD_PARSER = true;
  BUILD_TOKEN_MANAGER = true;
  SANITY_CHECK = true;
  FORCE_LA_CHECK = false;
}

PARSER_BEGIN(GMLParser)
package org.wilmascope.gmlparser;
import java.io.*;

public class GMLParser {
}

PARSER_END(GMLParser)

// Lexical definitions
TOKEN_MGR_DECLS: { static int nestingLevel = 0; }
   
<DEFAULT> SKIP :
{
  " " | "\t" | "\n" | "\r" | "\\"
|"Line" : IN_IGNORABLE
|"LabelGraphics" : IN_IGNORABLE
|"node_style" : IN_IGNORABLE
|"edge_style" : IN_IGNORABLE
|"style" : IN_IGNORABLE
}

<IN_IGNORABLE> SKIP:
{
  "[" { nestingLevel++; System.err.println("anon bracket, nestingLevel="+nestingLevel); }
| "]" { if (--nestingLevel == 0 ) SwitchTo(DEFAULT); System.err.println("nesting level="+nestingLevel);}
}

<IN_IGNORABLE> MORE:
{
  <~[]>
}

TOKEN:
{
  < GRAPH: "graph" >
| < VERSION: "version" >
| < DIRECTED: "directed" >
| < NAME: "name" >
| < NODE: "node" >
| < EDGE: "edge" >
| < ID: "id" >
| < LABEL: "label" >
| < SOURCE: "source" >
| < TARGET: "target" >
| < NUM: ( ["0"-"9"] )+ >
| < DEC: "." <NUM> | <NUM> ( "." <NUM> )?  >
| < GRAPHICSARG: ["a"-"z"]( " " | "\t" | "\n" | "\r" )+ <DEC> >
| < CTLSTRING: "\""(~["\""])*"\""> {matchedToken.image = matchedToken.image.substring(1, lengthOfMatch - 1); } 
} 

// end lexical definitions, parser start
void graph(GraphClient g) : {}
{
  <GRAPH> "[" stmtList(g) "]"
  <EOF> 
}

void stmtList(GraphClient g) : {String s;}
{
	{
	  System.out.println("Parsing the GML graph...");
	}
  ( stmt(g) )*
}

void stmt(GraphClient g) : { NodeClient n; Token t1, t2, t3, t4; }
{
  <VERSION> <NUM>
| <DIRECTED> <NUM>
| <NODE> "["
    ( <ID> t1=<NUM> <LABEL> t2=<CTLSTRING> ) 
    { g.addNode(t1.image,t2.image); }
    "graphics" "[" (<GRAPHICSARG>)* "]"
  "]"
| <EDGE> "[" { t3 = null; }
    ( <SOURCE> t1=<NUM> <TARGET> t2=<NUM> ( <LABEL> t3=<CTLSTRING> )?
      "graphics" "[" "arrow" t4=<CTLSTRING> "]" )
    { if(t3==null) {
        g.addEdge(t1.image, t2.image, t4.image);
      } else {
        g.addEdge(t1.image,t2.image,t3.image,t4.image); 
      }
    }
  "]"
  
}

